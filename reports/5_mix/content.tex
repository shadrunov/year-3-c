\section{Цель работы}
Написать программу, в которой создается одномерный числовой массив. После заполнения значениями (случайными числами) массива в нем нужно выполнить циклическую перестановку элементов. Количество позиций для циклической перестановки вводится пользователем с клавиатуры.
Собрать программу с помощью компилятора Visual Studio и синтаксисом Intel.



\section{Ход работы}

\subsection{Описание алгоритма}

Программа должна осуществить циклический сдвиг массива из $n$ элементов на $shift$ позиций вправо. $0 < shift < n$, $n > 1$.

Сдвиг состоит из трёх этапов:
\begin{enumerate}
    \item Сначала в стек складываются последние $shift$ элементов;
    \item Затем первые $n - shift$ сдвигаются на $shift$ вправо;
    \item В конце первые $shift$ элементов заполняются из стека.
\end{enumerate}

Алгоритм сдвига реализован в ассемблерной вставке, создание и заполнение массива, а также ввод пользовательских данных — на языке С \cite{atnt2}.

\subsection{Компилятор gcc}

Код файла на С приведён в приложении A. Компилятор gcc использует транслятор gas, который использует синтаксис AT\&T \cite{atnt}. Приведём ассемблерную вставку в листинге 1.

\begin{lstlisting}[language={[x86masm]Assembler}, caption={Ассемблерная вставка на синтаксисе AT\&T}]
.text
.global f
.type f, @function

f:
    pushq %rdi  # array
    pushq %rsi  # n
    pushq %rdx  # shift
    pushq %rcx
    pushq %rbx


    # store last (shift) items of array to stack

    # init for loop
    movq $0, %rcx
    movl %edx, %ecx
    
    array_to_stack:
        # find position (number of elements - iterations left)
        pushq %rsi
        sub %ecx, %esi  # esi = n - items left
        movl (%rdi, %rsi, 4), %ebx  # ebx = array[n - items left]
        popq %rsi

        pushq %rbx  # store element array[n - items left] to stack
        loop array_to_stack


    # move first (n - shift) elements to the end of array

    # init for loop
    pushq %rsi
    sub %edx, %esi  # esi = n - shift
    movq $0, %rcx
    movl %esi, %ecx
    popq %rsi

    move_elements:
        pushq %rcx

        # source element
        sub $1, %rcx  # items left - 1
        movl (%rdi, %rcx, 4), %ebx  # array[items left - 1]

        # destination element
        add %rdx, %rcx  # rcx = shift + items left
        movl %ebx, (%rdi, %rcx, 4)

        popq %rcx
        loop move_elements


    # fill in the head of array with elements from stack

    # init for loop
    movq $0, %rcx
    movl %edx, %ecx

    return_from_stack:
        # get element
        popq %rbx

        # find position (iterations left)
        pushq %rcx
        sub $1, %ecx
        movl %ebx, (%rdi, %rcx, 4)  # ebx = array[items left - 1]
        popq %rcx

        loop return_from_stack


    popq %rbx
    popq %rcx
    popq %rdx  # shift
    popq %rsi  # n
    popq %rdi  # array

    ret

\end{lstlisting}

Для вызова функции $f$ из программы на С используется следующее объявление (листинг 2):

\begin{lstlisting}[language={c}, caption={Объявление функции в С}, numbers=none]
            extern void f(int32_t *array, int32_t n, int32_t shift);
\end{lstlisting}

Для сборки используется команда: \texttt{gcc atnt.s main.c}

Далее продемонстрируем работу программы (рисунки 1-2).

\image{1.png}{Работа программы}{0.63}
\image{2.png}{Работа программы}{0.63}
\FloatBarrier

Исполняемый файл также можно открыть в отладчике \texttt{edb} (рисунки 3).

\image{3.png}{Ассемблерная вставка в отладчике}{1}
\FloatBarrier


\subsection{Компилятор MSVC}

Попробуем скомпилировать программу на Windows с помощью Visual Studio Build Tools. Компилятор MSVC использует транслятор masm для синтакиса Intel, поэтому нам нужно переписать ассемблерную вставку: убрать префикс, поменять местами источник и приёмник, убрать типы из команд, а также учитывать регистры при передаче параметров в соответствии с соглашением о вызовах \cite{agreement}.

Приведём ассемблерную вставку на синтакисе Intel в листинге 3.

\begin{lstlisting}[language={[x86masm]Assembler}, caption={Ассемблерная вставка на синтаксисе Intel}]
.CODE

f PROC
    push rcx  ; array
    push rdx  ; n
    push r8  ; shift
    push rsi
    push rbx

    ; move arguments to adequate registers
    push rcx
    push rdx
    push r8
    pop rdx
    pop rsi
    pop rdi


    ; store last (shift) items of array to stack

    ; init for loop
    mov rcx, 0
    mov ecx, edx
    
    array_to_stack:
        ; find position (number of elements - iterations left)
        push rsi
        sub esi, ecx  ; esi = n - items left
        mov ebx, [rdi + rsi * 4]  ; ebx = array[n - items left]
        pop rsi

        push rbx  ; store element array[n - items left] to stack
        loop array_to_stack


    ; move first (n - shift) elements to the end of array

    ; init for loop
    push rsi
    sub esi, edx  ; esi = n - shift
    mov rcx, 0
    mov ecx, esi
    pop rsi

    move_elements:
        push rcx

        ; source element
        sub rcx, 1  ; items left - 1
        mov ebx, [rdi + rcx * 4]  ; array[items left - 1]

        ; destination element
        add rcx, rdx  ; rcx = shift + items left
        mov [rdi + rcx * 4], ebx

        pop rcx
        loop move_elements


    ; fill in the head of array with elements from stack

    ; init for loop
    mov rcx, 0
    mov ecx, edx

    return_from_stack:
        ; get element
        pop rbx

        ; find position (iterations left)
        push rcx
        sub ecx, 1
        mov [rdi + rcx * 4], ebx  ; ebx = array[items left - 1]
        pop rcx

        loop return_from_stack


    pop rbx
    pop rsi
    pop r8  ; shift
    pop rdx  ; n
    pop rcx  ; array

    ret

f ENDP
END
\end{lstlisting}

Для компиляции и сборки программы используем Developer Command Prompt и следующие команды \cite{cli}:
\begin{itemize}
    \item \texttt{ml /c /Cx intel.s} (\url{https://stackoverflow.com/a/4549614})
    \item \texttt{cl intel.obj main.c}
\end{itemize} 

Сборка программы показана на рисунке 4. Работа программы — на рисунке 5.

\image{4.png}{Сборка программы}{1}
\image{5.png}{Работа программы}{1}
\FloatBarrier

Далее продемонстрируем работу программы в отладчике (рисунки 6-8). На последнем рисунке видно, что последние три элемента массива сохранены в стек.

\image{6.png}{Ассемблерная вставка в отладчике}{1}
\image{7.png}{Пользовательские данные в регистрах процессора}{0.63}
\image{8.png}{Элементы массива в дампе.}{0.8}
\FloatBarrier

\clearpage

\section{Выводы о проделанной работе}
В рамках данной работы я написал программу, в которой создается одномерный числовой массив. После заполнения значениями (случайными числами) массива в нем нужно выполнить циклическую перестановку элементов. Количество позиций для циклической перестановки вводится пользователем с клавиатуры. Собрал программу с помощью компилятора Visual Studio с синтаксисом Intel, а также с помощью компилятора gcc с синтаксисом AT\&T.

\clearpage